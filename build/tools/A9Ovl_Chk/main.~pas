unit main;

interface

uses
  Windows, Messages, SysUtils, Variants, Classes, Graphics, Controls, Forms,
  Dialogs, StdCtrls, FileCtrl, ComCtrls, ShellCtrls;

type
  TForm1 = class(TForm)
    ListBox_log: TListBox;
    Button1: TButton;
    DriveBox: TDriveComboBox;
    DirListBox: TDirectoryListBox;
    StatusBar: TStatusBar;
    StaticText1: TStaticText;
    StaticText2: TStaticText;
    StaticText3: TStaticText;
    procedure Button1Click(Sender: TObject);
    procedure DriveBoxChange(Sender: TObject);
    procedure DirListBoxChange(Sender: TObject);
    procedure FormCreate(Sender: TObject);
  private
    { Private 宣言 }
    function CheckSrlHeader:integer;
  public
    { Public 宣言 }
  end;

  TRom_Fat = record
    top:Longword;  // file region start
    bottom:Longword;  //         end;
  end;

  TRomh = record //ROMヘッダ(nitro_romheader.hより)
    title: array[1..12] of Char;
    game_code: array[1..4] of Char;
//0x10
    maker_code: array[1..2] of Byte;
    machine_code: Byte;
    rom_type: Byte;
    rom_size: Byte;
    resv_A: array[1..8] of Byte;  //Reserve(All 0)
    region: Byte;
        // b0 = china,b1 = koria

    rom_version: Byte;
    comp_boot_area: Byte;//b7 =A9,b6=A7

    //
    // 0x020 for Static modules (Section:B)
    //ARM9
    A9_main_rom_offset: Longword;           // ROM offset
    A9_main_entry_address: Longword;        // Entry point
    A9_main_ram_address: Longword;          // RAM address
    A9_main_size: Longword;                 // Module size

    //ARM7
    A7_main_rom_offset: Longword;           // ROM offset
    A7_main_entry_address: Longword;        // Entry point
    A7_main_ram_address: Longword;          // RAM address
    A7_main_size: Longword;                 // Module size

//0x40
    dumy2:array[1..8] of Byte;//ツールで使用しない部分

    //
    // 0x048 for File Allocation Table[FAT] (Section:E)
    //
    fat_offset: Longword;        // ROM offset
    fat_size:Longword;           // Table size

    //
    // 0x050 for Overlay Tables[OVT] (Section:D)
    //
    //  ARM9
    main_ovt_offset: Longword;   // ROM offset
    main_ovt_size:Longword;             // Table size

  end;

var
  Form1: TForm1;
  SrlDir: String;//srlファイルの存在するディレクトリ名

implementation

function TForm1.CheckSrlHeader: integer;
var
  Rec: TSearchRec;
  s,ss: String;
  i,n,Count: integer;
  fsrl: file;
  romh: TRomh;
  noc_size,noc_sectors,size,sectors,nums,max_sectors,total_sectors: integer;
  fat: TRom_Fat;
  top,btm,all_sectors: Longword;

begin
  {$I-}
  Count :=0;
  //指定ディレクトリからファイルを取り出す
  // ** 内容で判別しないので違うファイル置かないよう注意 **
  if FindFirst(SrlDir + '*.*', faAnyFile, Rec) = 0 then
  try
    repeat
       //フォルダやカレントディレクトリや親ディレクトリは対象外
       if not((Rec.Attr and faDirectory > 0)) and
             (Rec.Name <> '.') and (Rec.Name <> '..') then
         begin
           Inc(Count);
           ListBox_log.Items.Add(' ********************************** ');
           ListBox_log.Items.Add('file: '+Rec.Name);
           AssignFile (fsrl,SrlDir+Rec.Name);
           FileMode := fmOpenRead;//リード専用指定
           Reset(fsrl,1);//ファイル開く
           if IOResult = 0 then begin
             BlockRead(fsrl,romh,SizeOf(TRomh),n);//ヘッダリード
             if SizeOf(TRomh) <> n then
               s := ' file error'
             else begin
               //SetLength(s,12);
               s := '[            ( )]';
               for i:=1 to 12 do begin
                   if romh.title[i] = char(0) then break;
                   s[i+1] := romh.title[i];
               end;
               s[15] := romh.game_code[4];//idの４文字目(仕向地)
               ListBox_log.Items.Add(s); //title
               s := 'game_code =     ';
               for i:=1 to 4 do s[12+i] := romh.game_code[i];
               ListBox_log.Items.Add(s); //game code
               ListBox_log.Items.Add('version = '+inttostr(romh.rom_version));
               ListBox_log.Items.Add(' '); //間をあける

             //オーバレイチェック
              if  romh.main_ovt_size >31 then begin
//DHT_OVERLAY_MAX     (512*1024)
                total_sectors := 0;
                all_sectors := 0;
                nums := romh.main_ovt_size div 32;//Ovl count
                noc_size :=0;//範囲外トータルsize
                noc_sectors := 0;// 同セクタ
                for  i := 0 to nums-1 do begin
                  top := 0;btm := 0;
                  seek( fsrl,romh.fat_offset+sizeof(TRom_Fat)*i);
                  BlockRead( fsrl,fat,sizeof(fat),n);//top,bottom取得
                  if n <> sizeof(fat) then begin
                    s := s+'Cannot read fat id='+ inttostr(i);
                    break;
                  end;
                  size := (fat.bottom - fat.top)+1;
                  sectors := size shr 9;//div 512
                  if (size and $1ff) <>0 then inc(sectors);
                  size := sectors shl 9;
                  inc(all_sectors,sectors);
                  //max_sectors = (DHT_OVERLAY_MAX/512 - total_sectors) / (nums-i);
                  if (total_sectors < 1024)then max_sectors := (1024 - total_sectors) div (nums-i)
                  else max_sectors := 0;
                  btm := fat.bottom;
                  if max_sectors = 0 then begin//残検証サイズなければ丸ごと検証外
                    top := fat.top;
                    ss := '(A9 Overlay の全部)';
                  end else begin
                    if sectors > max_sectors then begin //最大割当サイズ超
                      sectors := sectors - max_sectors;//検証されないセクタ数
                      top := fat.top + max_sectors;
                      inc(total_sectors,max_sectors);
                      ss := '(A9 Overlay の一部)';
                    end else begin //超過なし
                      inc(total_sectors,sectors);
                      sectors := 0;
                    end;
                  end;
                  if sectors > 0 then begin
                    size := sectors shl 9;
                    s := 'offset'+inttostr(i)+' = 0x'+ inttohex(top,8)
                         +' ; 0x'+ inttohex(top,8)
                         +'-0x' + inttohex(btm,8)+ss;
                    ListBox_log.Items.Add(s);
                    s := 'length'+ inttostr(i) + ' = 0x' + inttohex(size,4)
                        + ' ; '+ inttostr(size)+' bytes';
                    ListBox_log.Items.Add(s);
                    inc(noc_size,size);
                    inc(noc_sectors,sectors);
                  end;
                end;
                if i = nums then begin //中断なし
                  ListBox_log.Items.Add('total sectors = '+inttostr(all_sectors));
                  ListBox_log.Items.Add('no check sectors = '+inttostr(noc_sectors));
                  ListBox_log.Items.Add('no check size = '+inttostr(noc_size));
                  ListBox_log.Items.Add(' '); //間をあける
                  if noc_size > 0 then s:= 'NG'
                  else s:= 'OK';
                end;
              end else s := 'no OVL';
             end;
           end else s := ' Open Error';
           CloseFile(fsrl);
           ListBox_log.Items.Add(s);

         end;
    until (FindNext(Rec) <> 0);
  finally
    FindClose(Rec);
  end;
  {$I+}
  Result :=Count;
end;

{$R *.dfm}

procedure TForm1.Button1Click(Sender: TObject);
var
  FileCount :integer;
begin
  ListBox_log.Clear;
  //指定ディレクトリのsrlファイルをチェック
  FileCount := CheckSrlHeader;
  if FileCount>0 then begin
    ListBox_log.Items.Add(' ------------------------------ ');
    ListBox_log.Items.Add('total '+inttostr(FileCount)+' files');
  end else
    ListBox_log.Items.Add(' file not ditect');

end;



procedure TForm1.DriveBoxChange(Sender: TObject);
begin
  DirListBox.Drive := DriveBox.Drive;
end;

procedure TForm1.DirListBoxChange(Sender: TObject);
begin
  SrlDir := IncludeTrailingPathDelimiter(DirListBox.Directory);
  StatusBar.SimpleText := '[Target Dir] ' + DirListBox.Directory;
end;


procedure TForm1.FormCreate(Sender: TObject);
begin
  //イベント利用してSrlDirNameとステータスバーの設定
  DirListBox.Drive := DriveBox.Drive;
end;

end.
